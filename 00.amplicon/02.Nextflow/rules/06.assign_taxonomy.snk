rule classifer:
    input:
        os.path.join(config["results"]["table"], "rep_seqs.dada2.qza")
    output:
        os.path.join(config["results"]["taxonomy"], "taxonomy.dada2.qza")
    params:
        silva = config["params"]["taxonomy"]["silva"]
    log:
        os.path.join(config["logs"], "05.tax_classifer.silva.log")
    shell:
        '''
        qiime feature-classifier classify-sklearn \
            --i-classifier {params.silva} \
            --i-reads {input} --o-classification {output} 2>{log}
        '''

rule tabulate:
    input:
        os.path.join(config["results"]["taxonomy"], "taxonomy.dada2.qza")
    output:
        os.path.join(config["results"]["taxonomy"], "taxonomy.dada2.qzv")
    shell:
        '''
        qiime metadata tabulate \
            --m-input-file {input} --o-visualization {output} 
        '''

rule taxa_barplot:
    input:
        taxa     = os.path.join(config["results"]["taxonomy"], "taxonomy.dada2.qza"),
        table    = os.path.join(config["results"]["table"], "table.dada2.qza"),
        metadata = config["samples"]
    output:
        os.path.join(config["results"]["taxonomy"], "taxa.bar.plots.qzv")
    log:
        os.path.join(config["logs"], "05.taxa_bar_plot.log")
    shell:
        '''
        qiime taxa barplot \
            --i-table {input.table} \
            --i-taxonomy {input.taxa} \
            --m-metadata-file {input.metadata} \
            --o-visualization {output}  2>{log}
        '''
